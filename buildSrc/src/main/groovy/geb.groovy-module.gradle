plugins {
    id 'groovy'
    id 'java-library'
    id 'codenarc'
    id 'geb.base'
    id 'org.nosphere.apache.rat'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

configurations {
    groovy

    api.extendsFrom groovy
    codenarc.extendsFrom groovy
}

codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

def codenarcTasks = tasks.withType(CodeNarc)

tasks.register('codenarc') {
    dependsOn codenarcTasks
}

codenarcTasks.configureEach {
    exclude 'GebConfig*.groovy'
}

tasks.withType(GroovyCompile).configureEach {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

tasks.withType(Test).configureEach {
    shouldRunAfter codenarcTasks
}

tasks.named('rat') {
    excludes = [
                '.asf.yaml', // ASF metadata for github integration excluded from src zip
                '**/.gradle/**', '**/wrapper/**', 'gradlew*',  // gradle wrapper files excluded from src zip
                '**/build/**', '**/target/**', 'licenses/**', 'notices/**',
                '**/generatedResources/**',
                '**/node_modules/**',
                'out/**', '*.ipr', '**/*.iml', '*.iws', '.idea/**', // Intellij files
                '**/ratpack/public/robots.txt', // Doesn't support comments
                '**/ratpack/public/css/*.css', // site only
                '**/ratpack/public/js/*.js', // JavaScript files MIT license, site/test only
                '**/*.min.js', // JavaScript files MIT license, site/test only
                '**/*.min.css', // CSS files MIT license, site only
                '**/TransformedClass.template', // TODO: can we remove this?
                '**/icons.svg', // TODO: licensing info required
                '.classpath', '.project', '.settings/**', 'bin/**', // Eclipse files
                '**/MANIFEST.MF', // TODO: can we remove this?
                '**/META-INF/services/**', // TODO: can we remove this?
                '**/META-INF/groovy/**', // TODO: can we remove this?
                '**/hs_err_pid**.log', // sometimes left over error files
                '**/pnpm-lock.yaml', //Lock file for pnpm used to build the site
    ]
}